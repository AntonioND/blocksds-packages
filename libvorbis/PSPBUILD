# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=libvorbis
pkgver=1.1.2
pkgrel=1
pkgdesc="library for the multimedia container format, and the native file and stream format oggvorbis"
arch=('mips')
url="https://www.xiph.org/ogg/"
license=('BSD')
depends=('libogg')
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/psp-ports.git#commit=dbea68fa313ef5426a57d141928139e8abfb41c0")
sha256sums=('SKIP')

build() {
    cd "psp-ports/$pkgname"
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib" LIBS="-lc -lpspuser" ./autogen.sh --host psp --prefix=/psp
    make
}

package() {
    cd "psp-ports/$pkgname"
    make DESTDIR="${pkgdir}" install

    rm "${pkgdir}/psp/lib/libvorbis.la"
    rm "${pkgdir}/psp/lib/libvorbisenc.la"
    rm "${pkgdir}/psp/lib/libvorbisfile.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

