pkgname=sdl2-mixer
pkgver=2.6.3
pkgrel=2
pkgdesc="an audio mixer library based on the SDL2 library"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_mixer/"
license=('MIT')
depends=('sdl2' 'libxmp' 'libvorbis' 'libogg')
makedepends=()
optdepends=()
source=(
    "git+https://github.com/libsdl-org/SDL_mixer#commit=c9f0946a783293d12178ce86773dd6f8f1a08b1c"
    "main.c.sample"
    "CMakeLists.txt.sample"
    "test.ogg.sample"
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "$srcdir/SDL_mixer"
    sed -i 's#@prefix@\|@exec_prefix@#${PSPDEV}/psp#' SDL2_mixer.pc.in
    sed -i 's#@libdir@#${PSPDEV}/psp/lib#' SDL2_mixer.pc.in
    sed -i 's#@includedir@#${PSPDEV}/psp/include#' SDL2_mixer.pc.in
}

build() {
    cd "$srcdir/SDL_mixer"
    mkdir -p build && cd build
    cmake -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
          -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2MIXER_DEPS_SHARED=OFF -DSDL2MIXER_INSTALL=ON -DSDL2MIXER_SAMPLES=OFF \
          -DSDL2MIXER_FLAC=OFF -DSDL2MIXER_OPUS=OFF -DSDL2MIXER_MIDI=OFF \
          -DSDL2MIXER_VORBIS=VORBISFILE -DSDL2MIXER_VORBIS_VORBISFILE_SHARED=OFF \
          -DSDL2MIXER_MOD=ON -DSDL2MIXER_MOD_MODPLUG=OFF -DSDL2MIXER_MOD_XMP=ON ..
    make
}

package() {
    mkdir -p "$pkgdir/psp/sdk/samples/sdl2/mixer/"
    install -m 644 $srcdir/main.c.sample $pkgdir/psp/sdk/samples/sdl2/mixer/main.c
    install -m 644 $srcdir/CMakeLists.txt.sample $pkgdir/psp/sdk/samples/sdl2/mixer/CMakeLists.txt
    install -m 644 $srcdir/test.ogg.sample $pkgdir/psp/sdk/samples/sdl2/mixer/test.ogg

    cd "$srcdir/SDL_mixer/build"
    make --quiet $MAKEFLAGS install

    mv "$pkgdir/psp/share/licenses/SDL2_mixer" "$pkgdir/psp/share/licenses/$pkgname"
}
