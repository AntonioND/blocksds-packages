pkgname=openal
pkgver=1.21.1
pkgrel=3
pkgdesc="a cross-platform 3D audio API"
arch=('mips')
license=('gpl2')
url="https://openal-soft.org/"
makedepends=()
source=("https://github.com/kcat/openal-soft/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('8ac17e4e3b32c1af3d5508acfffb838640669b4274606b7892aa796ca9d7467f')

prepare() {
    cd $pkgname-soft-$pkgver
    sed -i 's#@prefix@#${PSPDEV}/psp#' openal.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' openal.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' openal.pc.in
    sed -i 's#@includedir@#${prefix}/include#' openal.pc.in
}

build() {
  cd $pkgname-soft-$pkgver
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp -DCMAKE_BUILD_TYPE=Release \
        -DALSOFT_UTILS=OFF -DALSOFT_EXAMPLES=OFF -DLIBTYPE=STATIC "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package () {
  cd "$pkgname-soft-$pkgver/build"
  make --quiet DESTDIR=$pkgdir $MAKEFLAGS install
  cd ..

  mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
  install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}
