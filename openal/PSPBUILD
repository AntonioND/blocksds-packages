pkgname=openal
pkgver=1.23.1
pkgrel=1
pkgdesc="a cross-platform 3D audio API"
arch=('mips')
license=('LGPL-2.0')
url="https://openal-soft.org/"
makedepends=()
source=("https://github.com/kcat/openal-soft/releases/download/${pkgver}/openal-soft-${pkgver}.tar.bz2")
sha256sums=('796f4b89134c4e57270b7f0d755f0fa3435b90da437b745160a49bd41c845b21')

prepare() {
    cd $pkgname-soft-$pkgver
    sed -i 's#@prefix@#${PSPDEV}/psp#' openal.pc.in
    sed -i 's#@exec_prefix@#${prefix}#' openal.pc.in
    sed -i 's#@libdir@#${prefix}/lib#' openal.pc.in
    sed -i 's#@includedir@#${prefix}/include#' openal.pc.in
}

build() {
  cd $pkgname-soft-$pkgver
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp -DCMAKE_BUILD_TYPE=Release \
        -DALSOFT_UTILS=OFF -DALSOFT_EXAMPLES=OFF -DLIBTYPE=STATIC "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package () {
  cd "$pkgname-soft-$pkgver/build"
  make --quiet DESTDIR=$pkgdir $MAKEFLAGS install
  cd ..

  mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
  install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
  install -m 644 BSD-3Clause "$pkgdir/psp/share/licenses/$pkgname"
}
