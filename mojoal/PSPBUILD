# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=mojoal
pkgver=r129.a4eb6c8
pkgrel=1
pkgdesc="an SDL2-based implementation of OpenAL"
arch=('mips')
url="https://github.com/icculus/mojoAL"
license=('zlib')
depends=()
makedepends=('psp-cmake')
optdepends=()
provides=('openal')
source=(
    "git+https://github.com/icculus/mojoAL.git"
    "CMakeLists.txt"
)
sha256sums=(
    'SKIP'
    'SKIP'
)

pkgver() {
  cd mojoAL
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd mojoAL
    cp -v ../CMakeLists.txt CMakeLists.txt
}

build() {
    cd mojoAL
    psp-cmake .
    make
}

package() {
    cd mojoAL
    mkdir -m 755 -p "${pkgdir}/psp/include/mojoal/AL" "${pkgdir}/psp/lib"
    install -m 644 "libmojoal.a" "${pkgdir}/psp/lib"
    install -m 644 "AL/alc.h" "AL/al.h" "${pkgdir}/psp/include/mojoal/AL"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE.txt "$pkgdir/psp/share/licenses/$pkgname"
}
