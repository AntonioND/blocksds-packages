pkgname=sdl2-ttf
pkgver=2.21.0
pkgrel=1
pkgdesc="a companion library to SDL2 for working with TrueType (tm) fonts"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_ttf/"
license=('MIT')
depends=('sdl2' 'freetype2')
makedepends=()
optdepends=()
source=(
    "git+https://github.com/libsdl-org/SDL_ttf.git#commit=fb8767cc692dcba6a3dbc020e71b74bf7bb993c5"
    "main.c"
    "CMakeLists.txt"
    "Pacifico.ttf"
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

prepare() {
    cd "$srcdir/SDL_ttf"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL2_ttf.pc.in
}

build() {
  cd "$srcdir/SDL_ttf"
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp \
    -DBUILD_SHARED_LIBS=OFF -DSDL2TTF_SAMPLES=OFF -DSDL2TTF_VENDORED=OFF -DCMAKE_POSITION_INDEPENDENT_CODE=OFF \
    -DSDL2TTF_INSTALL=ON "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
  mkdir -p "$pkgdir/psp/sdk/samples/sdl2/ttf/"
  mv main.c "$pkgdir/psp/sdk/samples/sdl2/ttf/"
  mv CMakeLists.txt "$pkgdir/psp/sdk/samples/sdl2/ttf/"
  mv Pacifico.ttf "$pkgdir/psp/sdk/samples/sdl2/ttf/"

  cd "$srcdir/SDL_ttf/build"
  make --quiet DESTDIR=$pkgdir $MAKEFLAGS install

  mv "$pkgdir/psp/share/licenses/SDL2_ttf" "$pkgdir/psp/share/licenses/$pkgname"
}
