pkgname=sdl2-ttf
pkgver=2.0.18
pkgrel=1
pkgdesc="a companion library to SDL2 for working with TrueType (tm) fonts"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_ttf/"
license=('MIT')
depends=('sdl2' 'freetype2')
makedepends=()
optdepends=()
source=("https://github.com/libsdl-org/SDL_ttf/archive/refs/tags/release-${pkgver}.tar.gz")
sha256sums=('6b61544441b72bdfa1ced89034c6396fe80228eff201eb72c5f78e500bb80bd0')

prepare() {
  cd "SDL_ttf-release-$pkgver"
  sed -i '/POSITION_INDEPENDENT_CODE ON/d' CMakeLists.txt
}

build() {
  cd "SDL_ttf-release-$pkgver"
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
    "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
  cd "SDL_ttf-release-$pkgver/build"
  make --quiet $MAKEFLAGS install

  mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
  install -m644 ../COPYING.txt "$pkgdir/psp/share/licenses/$pkgname"
}

