pkgname=sdl2-ttf
pkgver=2.0.19
pkgrel=1
pkgdesc="a companion library to SDL2 for working with TrueType (tm) fonts"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_ttf/"
license=('MIT')
depends=('sdl2' 'freetype2')
makedepends=()
optdepends=()
source=("git+https://github.com/libsdl-org/SDL_ttf.git#commit=443fb12a8c9edde076beaa474071a3879a7829d3")
sha256sums=('SKIP')

build() {
  cd "SDL_ttf"
  mkdir -p build
  cd build
  cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp -DBUILD_SHARED_LIBS=OFF \
    "${XTRA_OPTS[@]}" .. || { exit 1; }
  make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
  cd "SDL_ttf/build"
  make --quiet DESTDIR=$pkgdir $MAKEFLAGS install

  mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
  install -m644 ../COPYING.txt "$pkgdir/psp/share/licenses/$pkgname"
}

