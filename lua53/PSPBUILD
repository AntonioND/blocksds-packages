pkgname=lua53
pkgver=5.3.6
pkgrel=1
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language (version 5.3)"
arch=('mips')
url="https://lua.org"
license=('MIT')
depends=()
makedepends=()
optdepends=()
source=(
    "https://www.lua.org/ftp/lua-${pkgver}.tar.gz"
    "lua-${pkgver}-PSP.patch"
)
sha256sums=(
    'fc5fd69bb8736323f026672b1b7235da613d7177e72558893a0bdcd320466d60'
    'SKIP'
)

prepare() {
    cd "lua-${pkgver}"
    patch -p1 < ../lua-${pkgver}-PSP.patch

    sed -i 's/liblua.a/liblua53.a/' Makefile src/Makefile
}

build() {
    cd "lua-${pkgver}"
    make psp

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
}

package() {
    cd "lua-${pkgver}"
    make INSTALL_TOP="${pkgdir}/psp" INSTALL_BIN="/tmp" TO_BIN="/dev/null" \
        INSTALL_INC="${pkgdir}/psp/include/lua53" install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}
