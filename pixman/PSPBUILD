pkgname=pixman
pkgver=0.40.0
pkgrel=2
pkgdesc="Pixman is a low-level software library for pixel manipulation"
arch=('mips')
url="http://pixman.org/"
license=('MIT')
depends=('libpng')
makedepends=()
optdepends=()
source=("https://xorg.freedesktop.org/archive/individual/lib/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6d200dec3740d9ec4ec8d1180e25779c00bc749f94278c8b9021f5534db223fc')

build() {
    cd "${pkgname}-${pkgver}"
    autoreconf -if
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib $(psp-pkgconf --libs libpng zlib)" \
    CFLAGS="-G0 -O2 $(psp-pkgconf --cflags libpng zlib)" \
    ./configure --disable-loongson-mmi --host=psp --prefix=/psp
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "${pkgname}-${pkgver}"
    make --quiet $MAKEFLAGS DESTDIR="$pkgdir/" install || { exit 1; }

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING "$pkgdir/psp/share/licenses/$pkgname"
}

