pkgname=jpeg
pkgver=2.1.1
pkgrel=2
pkgdesc="a free library for JPEG image compression"
arch=('mips')
url="https://ijg.org/"
license=('custom')
depends=()
makedepends=()
optdepends=()
source=("https://github.com/libjpeg-turbo/libjpeg-turbo/archive/${pkgver}.tar.gz")
sha256sums=('20e9cd3e5f517950dfb7a300ad344543d88719c254407ffb5ad88d891bf701c4')

build() {
    cd "libjpeg-turbo-${pkgver}"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF \
        -DCMAKE_BUILD_TYPE=Release -DENABLE_SHARED=FALSE -DWITH_SIMD=0 "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "libjpeg-turbo-${pkgver}/build"
    make --quiet $MAKEFLAGS install
    cd ..

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 README.ijg "$pkgdir/psp/share/licenses/$pkgname"

    # Build system produces a bunch of ELF binaries we never use, can't disable it, just delete them
    rm -f "$pkgdir/psp/bin/tjbench"
    rm -f "$pkgdir/psp/bin/cjpeg"
    rm -f "$pkgdir/psp/bin/djpeg"
    rm -f "$pkgdir/psp/bin/jpegtran"
    rm -f "$pkgdir/psp/bin/rdjpgcom"
    rm -f "$pkgdir/psp/bin/wrjpgcom"
}

