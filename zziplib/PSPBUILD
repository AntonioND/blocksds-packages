# Maintainer: Wouter Wijsman <wwijsman@live.nl>
pkgname=zziplib
pkgver=0.13.38
pkgrel=1
pkgdesc="provides read access to zipped files in a zip-archive"
arch=('mips')
url="http://zziplib.sourceforge.net/"
license=('ZLIB')
depends=('zlib')
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/psp-ports.git#commit=5ee159bccf6b9539d69b5f43bba0eff0c75e6f11")
sha256sums=('SKIP')

build() {
    cd "psp-ports/$pkgname"
    LDFLAGS="$LDFLAGS -L$(psp-config --pspsdk-path)/lib -L$(psp-config --psp-prefix)/lib -lc -lpspuser" \
    LIBS="$LIBS -lc -lpspuser" \
    ./configure --host=psp --prefix="/psp"
    make
}

package() {
    cd "psp-ports/$pkgname"
    make DESTDIR="${pkgdir}" install

    rm "${pkgdir}/psp/lib/libzzip.la"
    rm "${pkgdir}/psp/lib/libzzipfseeko.la"
    rm "${pkgdir}/psp/lib/libzzipmmapped.la"
    rm "${pkgdir}/psp/lib/libzzipwrap.la"

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 COPYING.LIB "$pkgdir/psp/share/licenses/$pkgname"
}

