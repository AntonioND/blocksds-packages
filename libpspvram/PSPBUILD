pkgname=libpspvram
pkgver=r7.20d1cf6
pkgrel=1
pkgdesc="Dynamic VRAM allocation manager for the PSP"
arch=('mips')
url="https://github.com/albe/libpspvram"
license=('MIT')
depends=()
makedepends=()
optdepends=()
source=("git+https://github.com/pspdev/libpspvram.git#commit=20d1cf60f9c9bf8fd804c1bab579b03be86b3591")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    # Set the revision of the repo as version
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$pkgname/build"
    make --quiet $MAKEFLAGS install

    cd ..
    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 LICENSE "$pkgdir/psp/share/licenses/$pkgname"
}

