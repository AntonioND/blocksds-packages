pkgname=sdl2-image
pkgver=2.7.0
pkgrel=1
pkgdesc="a simple library to load images of various formats as SDL surfaces"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_image/"
license=('zlib')
depends=('sdl2' 'libpng' 'jpeg')
makedepends=()
optdepends=()
source=("git+https://github.com/libsdl-org/SDL_image#commit=ede6056d844e7a1ecec3e688cb4068d1ad0aa884")
sha256sums=('SKIP')

prepare() {
    cd "$srcdir/SDL_image"
    sed -i 's#@prefix@#${PSPDEV}/psp#' SDL2_image.pc.in
}

build() {
    cd "$srcdir/SDL_image"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=/psp \
    -DBUILD_SHARED_LIBS=OFF -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2IMAGE_DEPS_SHARED=OFF \
    -DSDL2IMAGE_INSTALL=ON -DSDL2IMAGE_SAMPLES=OFF -DSDL2IMAGE_BACKEND_STB=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "$srcdir/SDL_image/build"
    make DESTDIR="$pkgdir" install

    mv "$pkgdir/psp/share/licenses/SDL2_image" "$pkgdir/psp/share/licenses/$pkgname"
}

