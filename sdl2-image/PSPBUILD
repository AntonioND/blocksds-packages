pkgname=sdl2-image
pkgver=2.6.3
pkgrel=1
pkgdesc="a simple library to load images of various formats as SDL surfaces"
arch=('mips')
url="https://www.libsdl.org/projects/SDL_image/"
license=('zlib')
depends=('sdl2' 'libpng' 'jpeg')
makedepends=()
optdepends=()
source=(
    "https://www.libsdl.org/projects/SDL_image/release/SDL2_image-${pkgver}.tar.gz"
	"pkg-config-fix.patch"
)
sha256sums=(
    "931c9be5bf1d7c8fae9b7dc157828b7eee874e23c7f24b44ba7eff6b4836312c"
    "0adc4fbfbeb89b653a8050253d7bfa64a4ab3c54555879af68934168a978e16c"
)
prepare() {
    cd "${srcdir}/SDL2_image-${pkgver}"
    patch -Np1 -i "${srcdir}/pkg-config-fix.patch"
}

build() {
    cd "${srcdir}/SDL2_image-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DCMAKE_POSITION_INDEPENDENT_CODE=OFF -DSDL2IMAGE_DEPS_SHARED=OFF \
        -DSDL2IMAGE_INSTALL=ON -DSDL2IMAGE_SAMPLES=OFF -DSDL2IMAGE_BACKEND_STB=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd "${srcdir}/SDL2_image-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" ${MAKEFLAGS} install

    mv "${pkgdir}/psp/share/licenses/SDL2_image" "${pkgdir}/psp/share/licenses/${pkgname}"
}

