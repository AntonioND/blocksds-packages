pkgname=sdl2
pkgver=2.0.20
pkgrel=1
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="https://github.com/libsdl-org/SDL"
license=('ZLIB')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=('sdl2-main')
source=("https://github.com/libsdl-org/SDL/archive/refs/tags/release-${pkgver}.tar.gz")
sha256sums=('2a026753af9b03fca043824bca8341f74921a836d28729e0c31aa262202a83c6')

prepare() {
    cd SDL-release-${pkgver}
    sed -i 's#@prefix@\|@exec_prefix@#${PSPDEV}/psp#' sdl2-config.in
    sed -i 's#@libdir@#${PSPDEV}/psp/lib#' sdl2-config.in
    sed -i 's#@includedir@#${PSPDEV}/psp/include#' sdl2-config.in
}

build() {
    cd SDL-release-${pkgver}
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd SDL-release-${pkgver}/build
    make --quiet $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../LICENSE.txt "$pkgdir/psp/share/licenses/$pkgname"
}

