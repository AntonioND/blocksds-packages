pkgname=sdl2
pkgver=2.30.2
pkgrel=4
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="https://wiki.libsdl.org/SDL2/FrontPage"
license=('ZLIB')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=('sdl2-main')
source=(
    "https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL2-${pkgver}.tar.gz"
    "https://github.com/libsdl-org/SDL/pull/9953.patch"
    "https://github.com/libsdl-org/SDL/pull/9970.patch"
    "https://github.com/libsdl-org/SDL/pull/9977.patch"
    "CMakeLists.txt.sample"
    "main.c.sample"
)
sha256sums=(
    "891d66ac8cae51361d3229e3336ebec1c407a8a2a063b61df14f5fdf3ab5ac31"
    "f465c3914901fa6e49e5389c8bb5ec2ede21a620c431b8fd6bc55e315ddb5523"
    "cc1c85198aa66023d0c3cb2117d855d699908da216357d195bfe4c8415ab7cbd"
    "ce36079fe087a3e7956aa719b0687c797ade2387943e8d23feb39489ba88fcb2"
    "SKIP"
    "SKIP"
)

prepare() {
    cd "${srcdir}/SDL2-${pkgver}"
    sed -i '/^prefix=/s/=.*$/=${PSPDEV}\/psp/' sdl2.pc.in

    patch -p1 < ../9953.patch
}

build() {
    cd "${srcdir}/SDL2-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DSDL_TESTS=OFF -DSDL_TEST=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    mkdir -p "${pkgdir}/psp/sdk/samples/sdl2/basic/"
    install -m 644 "${srcdir}/main.c.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/main.c"
    install -m 644 "${srcdir}/CMakeLists.txt.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/CMakeLists.txt"

    cd "${srcdir}/SDL2-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" $MAKEFLAGS install

    mv "${pkgdir}/psp/share/licenses/SDL2" "${pkgdir}/psp/share/licenses/${pkgname}"
}
