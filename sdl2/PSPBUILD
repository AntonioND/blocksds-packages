pkgname=sdl2
pkgver=2.28.0
pkgrel=1
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="https://github.com/libsdl-org/SDL"
license=('ZLIB')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=('sdl2-main')
source=(
    "https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL2-${pkgver}.tar.gz"
    "pkg-config-fix.patch"
    "CMakeLists.txt.sample"
    "main.c.sample"
)
sha256sums=(
    "d215ae4541e69d628953711496cd7b0e8b8d5c8d811d5b0f98fdc7fd1422998a"
    "4f3bf0dd850d778c4e496f5c4d1a9827d28aa094335ef5d8cca4ce36b62e3012"
    "47b2ce55d1e0a15a8d355c957576cc11eaf089638c5560ecb142d08fe90c74c6"
    "a14f3f8bd70f5be01d7e4230450558431230082deef064df30aaeeb34a73a33c"
)

prepare() {
    cd "${srcdir}/SDL2-${pkgver}"
    patch -Np1 -i "${srcdir}/pkg-config-fix.patch"
}

build() {
    cd "${srcdir}/SDL2-${pkgver}"
    mkdir -p build && cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE="${PSPDEV}/psp/share/pspdev.cmake" -DCMAKE_INSTALL_PREFIX=/psp \
        -DBUILD_SHARED_LIBS=OFF -DSDL_TESTS=OFF -DSDL_TEST=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    mkdir -p "${pkgdir}/psp/sdk/samples/sdl2/basic/"
    install -m 644 "${srcdir}/main.c.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/main.c"
    install -m 644 "${srcdir}/CMakeLists.txt.sample" "${pkgdir}/psp/sdk/samples/sdl2/basic/CMakeLists.txt"

    cd "${srcdir}/SDL2-${pkgver}/build"
    make --quiet DESTDIR="${pkgdir}" $MAKEFLAGS install

    mv "${pkgdir}/psp/share/licenses/SDL2" "${pkgdir}/psp/share/licenses/${pkgname}"
}
