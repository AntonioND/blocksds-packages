pkgname=sdl2
pkgver=2.0.21
pkgrel=1
pkgdesc="a library designed to provide low level access to audio, input, and graphics hardware"
arch=('mips')
url="https://github.com/libsdl-org/SDL"
license=('ZLIB')
depends=('libpspvram' 'pspgl')
makedepends=()
optdepends=()
provides=('sdl2-main')
source=("git+https://github.com/libsdl-org/SDL.git#commit=18032979d892639ad4e03584a40a4d205cd2f5fd")
sha256sums=('SKIP')

prepare() {
    cd SDL
    sed -i 's#@prefix@\|@exec_prefix@#${PSPDEV}/psp#' sdl2-config.in
    sed -i 's#@libdir@#${PSPDEV}/psp/lib#' sdl2-config.in
    sed -i 's#@includedir@#${PSPDEV}/psp/include#' sdl2-config.in
}

build() {
    cd SDL
    mkdir -p build
    cd build
    cmake -Wno-dev -DCMAKE_TOOLCHAIN_FILE=$PSPDEV/psp/share/pspdev.cmake -DCMAKE_INSTALL_PREFIX=${pkgdir}/psp -DBUILD_SHARED_LIBS=OFF "${XTRA_OPTS[@]}" .. || { exit 1; }
    make --quiet $MAKEFLAGS || { exit 1; }
}

package() {
    cd SDL/build
    make --quiet $MAKEFLAGS install

    mkdir -m 755 -p "$pkgdir/psp/share/licenses/$pkgname"
    install -m 644 ../LICENSE.txt "$pkgdir/psp/share/licenses/$pkgname"
}

