pkgname=lua
pkgver=5.2.0
pkgrel=1
pkgdesc="a powerful, efficient, lightweight, embeddable scripting language"
arch=('mips')
url="https://lua.org"
license=('MIT')
groups=('pspdev-default')
depends=()
makedepends=()
optdepends=()
source=(
    "https://www.lua.org/ftp/lua-${pkgver}.tar.gz"
    "lua-${pkgver}-PSP.patch"
)
sha256sums=(
    'cabe379465aa8e388988073d59b69e76ba0025429d2c1da80821a252cdf6be0d'
    'SKIP'
)

prepare() {
    cd "lua-${pkgver}"
    patch -p1 < "../lua-${pkgver}-PSP.patch"
}

build() {
    cd "lua-${pkgver}"
    make -f Makefile.psp

    # extract the license from readme
    grep -A21 Copyright doc/readme.html | \
        sed -e 's/&ndash;/–/g' \
            -e 's/&copy;/©/g' \
            -e '/<P>/d' > LICENSE
}

package() {
    cd "lua-${pkgver}"
    make -f Makefile.psp PSPDIR="${pkgdir}/psp" install

    mkdir -m 755 -p "${pkgdir}/psp/share/licenses/${pkgname}"
    install -m 644 LICENSE "${pkgdir}/psp/share/licenses/${pkgname}"
}
