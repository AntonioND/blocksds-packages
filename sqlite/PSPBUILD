pkgname=sqlite
pkgver=3.7.3
pkgrel=4
pkgdesc="SQLite is a C-language library that implements a small, fast, self-contained, high-reliability, full-featured, SQL database engine"
arch=('mips')
url="https://www.sqlite.org/"
license=('custom:Public Domain')
depends=()
optdepends=()
makedepends=()
source=(
    "https://www.sqlite.org/src/tarball/26778480/SQLite-26778480.tar.gz"
    "${pkgname}-${pkgver}-PSP.patch"
)
sha256sums=(
    "bb49ad9c7d0e10f9c0862fdfc57dd6bfe5164b478a464179f3d34ca847a46dc6"
    "SKIP"
)

prepare() {
    cd "SQLite-26778480"
    patch -p1 < ../${pkgname}-${pkgver}-PSP.patch
}

build() {
    cd "SQLite-26778480"
    LDFLAGS="-L$(psp-config --pspsdk-path)/lib -lc -lpspuser" \
    CFLAGS="-G0 -O2 -DSQLITE_OS_OTHER=1 -DSQLITE_OS_PSP=1 -I$(psp-config --pspsdk-path)/include" \
    ./configure --host=psp --prefix="/psp" --disable-readline --disable-tcl --disable-threadsafe --disable-amalgamation
    make
}

package() {
    cd "SQLite-26778480"
    make DESTDIR="$pkgdir" install

    rm "${pkgdir}/psp/lib/libsqlite3.la"
}
